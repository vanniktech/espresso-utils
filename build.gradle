apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    supportLibrary: '27.0.1',
    espresso: '3.0.1',
  ]

  ext.deps = [
    'support': [
      'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
      'appcompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",

      'test': [
        'espresso': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
      ],
    ],
    'test': [
      'privateconstructor': 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
      'junit': 'junit:junit:4.12',
      'assertj3': 'org.assertj:assertj-core:3.8.0',
    ],
  ]

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.8.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.10.0'

    classpath 'com.android.tools.build:gradle:3.0.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.4'
  }
  pmd {
    toolVersion = '5.8.1'
  }
  ktlint {
    toolVersion = '0.12.0'
  }
  detekt {
    toolVersion = '1.0.0.RC5-3'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 27
  targetSdkVersion = 27

  versionCode = VERSION_CODE
  versionName = VERSION_NAME
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3'
  distributionType = Wrapper.DistributionType.ALL
}
