apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    supportLibrary: '26.0.1',
    testRules: '1.0.0',
    espresso: '3.0.0',
  ]

  ext.deps = [
    'support': [
      'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
      'appcompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",

      'test': [
        'espresso': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
      ],
    ],
    'test': [
      'privateconstructor': 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
      'junit': 'junit:junit:4.12',
      'assertj3': 'org.assertj:assertj-core:3.8.0',
    ],
  ]

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.8.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.8.0'

    classpath 'com.android.tools.build:gradle:2.3.3'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.1'
  }
  pmd {
    toolVersion = '5.8.1'
  }
  ktlint {
    toolVersion = '0.9.1'
  }
  detekt {
    toolVersion = '1.0.0.RC2'
  }
  errorProne {
    toolVersion = '2.0.21'
  }
}

subprojects {
  repositories {
    jcenter()
    google()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 26
  targetSdkVersion = 26
  buildToolsVersion = '26.0.1'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.1'
  distributionType = Wrapper.DistributionType.ALL
}
