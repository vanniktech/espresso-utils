apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    espresso: '3.1.1',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'support': [
      'annotations': 'androidx.annotation:annotation:1.0.1',
      'appCompat': 'androidx.appcompat:appcompat:1.0.0',

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
        ],
        'junitExt': "androidx.test.ext:junit:1.1.0",
        'runner': "androidx.test:runner:${versions.testRules}",
        'rules': "androidx.test:rules:${versions.testRules}",
      ],
    ],
    'test': [
      'privateconstructor': 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
      'junit': 'junit:junit:4.13-beta-1',
      'assertj3': 'org.assertj:assertj-core:3.11.1',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0'

    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.6.0'
  }
}

codeQualityTools {
  lint {
    textReport = true
  }
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.1.2'
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

ext {
  minSdkVersion = 15
  compileSdkVersion = 28
  targetSdkVersion = 28

  versionCode = VERSION_CODE
  versionName = VERSION_NAME
}

wrapper {
  gradleVersion = '4.10.3'
  distributionType = Wrapper.DistributionType.ALL
}
